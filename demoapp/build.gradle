apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.monits.staticCodeAnalysis'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/gradle/dexcount.gradle"

android {
    compileSdkVersion maxApiLevel.toInteger()
    buildToolsVersion androidBuildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "$libraryGroupId" + ".demoapp"
        minSdkVersion minApiLevel
        targetSdkVersion maxApiLevel
        versionCode "$demoAppVersionCode".toInteger()
        versionName "$libraryVersion"

        multiDexEnabled true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            keyAlias "ci-key-alias"
            keyPassword "andesui"
            storeFile file("key-debug.keystore")
            storePassword "andesui"
        }
        release {
            keyAlias "ci-key-alias"
            keyPassword "andesui"
            storeFile file("key-release.keystore")
            storePassword "andesui"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            aaptOptions {
                cruncherEnabled false
            }

            resValue("string", "andesui_demoapp_app_name", "Andes DEBUG v${libraryVersion}")
            signingConfig signingConfigs.debug

        }
        release {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true

            applicationIdSuffix ''
            resValue("string", "andesui_demoapp_app_name", "Andes v${libraryVersion}")
            signingConfig signingConfigs.release
        }
        mds {
            initWith(buildTypes.debug)

            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (project.hasProperty('mdsAppIdSuffix')) {
                applicationIdSuffix "${project.mdsAppIdSuffix}"
            }
            if (project.hasProperty('mdsVersionNameSuffix')) {
                versionNameSuffix "${project.mdsVersionNameSuffix}"
            }
            resValue("string", "andesui_demoapp_app_name", "Andes v${libraryVersion}")
        }
    }

    lintOptions {
        // TODO: Delete this once play services is updated. Now we need it because play services brings support library 26
        disable 'GradleCompatible'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude("META-INF/*.kotlin_module")
    }

    dexOptions.preDexLibraries = true
}

staticCodeAnalysis {
    spotbugs = false
    // False is REQUIRED in order to make CI server fail the build on errors.
    ignoreErrors = false

    androidLintConfig = "$rootDir/config/android/android-lint-demoapp.xml"
}

dependencies {
    implementation project(':components')
    implementation project(':coachmark')

    implementation prod.kotlinStdlib
    implementation prod.supportConstraintLayout
    implementation prod.supportLibraryAppCompatV7
    implementation showcase.coordinatorLayout

    implementation prod.fresco

    implementation showcase.glide
    implementation showcase.markwon

    implementation platform(firebase.bom)
    implementation firebase.analytics

    debugImplementation showcase.leakCanary
}

/**
 * Based on https://stackoverflow.com/a/44218054/2823516
 */
copy {
    from file('../CHANGELOG.md')
    into 'src/main/res/raw'
    rename { String fileName ->
        fileName.replace("CHANGELOG.md", "andesui_demoapp_changelog.md")
    }
}
